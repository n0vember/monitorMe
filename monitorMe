#!/usr/bin/env perl

use strict;
use Getopt::Long;

my %conf;

($conf{my_name} = $0 ) =~ s:.*/::;
$conf{config_file}="";
$conf{verbose_mode}=0;
$conf{quiet_mode}=0;
$conf{exit_code}=0;

sub logMsg { print @_,"\n" if ! $conf{quiet_mode}; }
sub errMsg { print STDERR ++$conf{exit_code}," ",@_,"\n" if ! $conf{quiet_mode}; }
sub logVrb { logMsg(@_) if $conf{verbose_mode}; }
sub errVrb { errMsg(@_) if $conf{verbose_mode}; }

sub usage {
  errMsg @_ if @_;
  logMsg if @_;
  logMsg "usage: $conf{my_name} --config file [--verbose ] [--quiet]";
  logMsg "   or: $conf{my_name} --usage [ --verbose ]";
  logMsg "  --config : specify config file";
  logVrb "             Config file is composed of shell commands, one per line.";
  logVrb "             Each command is followed by a comment that will be displayed";
  logVrb "             if the command line fails (exit code != 0)";
  logMsg "  --usage : displays this help (--verbose produces more)";
  logMsg "  --verbose : enables verbose mode";
  logMsg "  --quiet : does not produce any output";
  exit $conf{exit_code};
}

GetOptions (
    "config=s" => \$conf{config_file},
    "verbose" => \$conf{verbose_mode},
    "quiet" => \$conf{quiet_mode},
    "usage" => sub { usage; },
  ) or die("Error in command line arguments\n");

usage "--quiet and --verbose options are exclusive." if $conf{verbose_mode} and $conf{quiet_mode};
logVrb "Verbose mode is activated.";
usage "config file is mandatory." if ! $conf{config_file};
usage "$conf{config_file} does not exist." if ! -f $conf{config_file};

open CONF,"<$conf{config_file}";
while (<CONF>) {
  chomp;
  logVrb "current line is : $_";
  (my $command, my $message)=split /#/;
  $command=~ s: *$: >/dev/null 2>&1:;
  $message=~ s/^ *//;
  logVrb "  command is : $command";
  logVrb "  message is : $message";
  system($command) == 0 or errMsg $message;
}
close CONF;

exit $conf{exit_code};
